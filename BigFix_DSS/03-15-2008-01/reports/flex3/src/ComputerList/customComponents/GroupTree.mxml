<?xml version="1.0" encoding="utf-8"?>
<mx:Panel title="Computer Groups" xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" creationComplete="computerGroups.getTree.send()">
	<mx:Script><![CDATA[
		import mx.controls.Alert;
		import mx.events.ListEvent;
		import com.bigfix.dss.view.general.Alert.AlertPopUp;
		//import mx.events.CollectionEvent;

		private var _initialComputerGroupID:int;
		private var _openItems:Array;

		public function set initialComputerGroupID(value:int):void {
			_initialComputerGroupID = value;
		}

		public function get selected_id():Number {
			return tree.selectedItem.id;
		}

		private function setOpenItems():void {
			_openItems = new Array();
			if (_initialComputerGroupID) {
				for (var i:int = 0; i < computerGroups.getTree.lastResult.length; i++) {
					appendOpenItems(computerGroups.getTree.lastResult[i]);
				}
			} else {
				_openItems.push(computerGroups.getTree.lastResult[0]);
			}
			tree.openItems = _openItems;
			tree.selectedItem = _openItems[_openItems.length - 1];
			dispatchEvent(new ListEvent('change'));
		}

		private function appendOpenItems(node:Object):Boolean {
			_openItems.push(node);
			if (node.id == _initialComputerGroupID) {
				return true;
			}
			if (node.children) {
				for (var i:int = 0; i < node.children.length; i++) {
					if (appendOpenItems(node.children[i])) {
						return true;
					}
				}
			}
			_openItems.pop();
			return false;
		}
	]]></mx:Script>

	<mx:Metadata>
		[Event(name='change', type='mx.events.ListEvent')]
	</mx:Metadata>

	<mx:RemoteObject id="computerGroups" destination="ComputerGroups" showBusyCursor="true" fault="AlertPopUp.error(event.fault.faultString, 'Error');">
		<mx:method name="getTree" result="setOpenItems()"/>
	</mx:RemoteObject>
	<mx:Tree id="tree" width="100%" height="100%"
		showRoot="false"
		dataProvider="{computerGroups.getTree.lastResult}"
		labelField="name"
		change="dispatchEvent(event)"
	/>
</mx:Panel>
